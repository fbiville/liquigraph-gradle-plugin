group = 'org.liquigraph'
version = '2.0.0-SNAPSHOT'

def neo4jVersion = "2.3.9"

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.6"
  }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"

repositories {
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    mavenLocal()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.18'

    testCompile 'junit:junit:4.12'
    
    compile 'org.liquigraph:liquigraph-core:2.0.2-SNAPSHOT'
    compile gradleApi()

    testCompile gradleTestKit()
    testCompile "org.neo4j:neo4j:${neo4jVersion}"
    testCompile "org.neo4j:neo4j-kernel:${neo4jVersion}:tests"
    testCompile "org.neo4j:neo4j-io:${neo4jVersion}:tests"
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/classes/test")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        def classpathSeparator = System.getProperty("path.separator")
        def pluginClasspath = file("$outputDir/plugin-under-test-metadata.properties")
        def classpaths = sourceSets.main.runtimeClasspath.join(classpathSeparator)
        classpaths +=    classpathSeparator + sourceSets.test.resources.collect { it.getParent() }.join(classpathSeparator)
        pluginClasspath.text = "implementation-classpath=" + classpaths
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

pluginBundle {
  website = 'https://liquigraph.github.io/'
  vcsUrl = 'https://github.com/liquigraph/liquigraph-gradle-plugin'
  description = 'A Gradle plugin for Liquigraph (graph migrations made easy).'
  tags = ['liquigraph', 'neo4j', 'graph', 'migration']

  plugins {
    greetingsPlugin {
      id = 'org.liquigraph.gradle'
      displayName = 'Liquigraph Gradle plugin'
    }
  }
}
